plugins {
    id 'com.android.application'
}

android {
    compileSdk 33
    //buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.trump.myxposed"
        minSdkVersion 22
        targetSdkVersion 33
        versionCode 4
        versionName "1.3"

    }

    def properties
    def propertiesFile = rootProject.file('signing.properties')
    if (propertiesFile.exists()) {
        properties = new Properties()
        properties.load(propertiesFile.newDataInputStream())
    }
    def getString = { String propertyName, String environmentName, String prompt ->
        return properties?.get(propertyName) ?: System.getenv(environmentName) ?:
            System.console()?.readLine('\n' + prompt + ': ')
    }
    def keyStoreFile = getString('storeFile', 'STORE_FILE', 'Store file')?.with(rootProject.&file)

    signingConfigs {
        release {
            if (keyStoreFile != null) {
                storeFile keyStoreFile
                storePassword getString('storePassword', 'STORE_PASSWORD', 'Store password')
                keyAlias getString('keyAlias', 'KEY_ALIAS', 'Key alias')
                keyPassword getString('keyPassword', 'KEY_PASSWORD', 'Key password')
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    implementation 'com.github.zhaokaiqiang.klog:library:1.6.0'

    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    // For developers using AndroidX in their applications
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.google.code.gson:gson:2.8.9'
}
